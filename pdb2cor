#!/usr/bin/awk -f
BEGIN{ 
   natom = 0; 
   maxresid = 0;
   iresid = 0;
   iresind =0;
}
/^REMARK/ { 
   remark = substr($0,7,60);
   printf("* %s \n",remark);
}
/^HETATM/ {
    atomname[++natom] = substr($0,13,4);
    sub(/^[ \t]+/, "",atomname[natom]);
    resname[natom]    = substr($0,18,4);
    sub(/^[ \t]+/, "",resname[natom]);
    resid[natom]      = substr($0,23,4);
    segname[natom]    = substr($0,73,4)

    if (resname[natom] != resname[natom-1]) { iresid++; }
    else {
       if (resid[natom]   != resid[natom-1]){ iresid++; }
    }
    resid_new[natom] = iresid;

    x[natom] = substr($0,32,7);
    y[natom] = substr($0,40,7);
    z[natom] = substr($0,48,7);
}

/^ATOM/ {
    atomname[++natom] = substr($0,13,4);
    sub(/^[ \t]+/, "",atomname[natom]);
    resname[natom]    = substr($0,18,4);
    sub(/^[ \t]+/, "",resname[natom]);
    resid[natom]      = substr($0,23,4);
    resind[natom]     = substr($0,23,4);
    segname[natom]    = substr($0,73,4)

    if (resname[natom] != resname[natom-1]) { iresid++; }
    else {
       if (resid[natom]   != resid[natom-1]){ iresid++; }
    }
    resid_new[natom] = iresid;

    if (segname[natom] != segname[natom-1]) { iresind=1; }
    else {
       if (resid[natom]   != resid[natom-1]){ iresind++; }
    }
    resind_new[natom] = iresind;

    x[natom]          = substr($0,32,7);
    y[natom]          = substr($0,40,7);
    z[natom]          = substr($0,48,7);
}
END{
    printf("%10s%5s\n",natom,"EXT");
   for (i=1; i<=natom;i++)
   {
       printf("%10s%10s ",i,resid_new[i]);
       printf(" %-10s%-13s",resname[i],atomname[i]);
       printf("% 15.10f     % 15.10f     % 15.10f ",x[i],y[i],z[i]);
       printf(" %4s      %-10s",segname[i],resind_new[i]);
       printf("      %12.10f\n",0.0);
   }
}
